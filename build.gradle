//file:noinspection GroovyAssignabilityCheck
//file:noinspection ConfigurationAvoidance
//file:noinspection GrUnresolvedAccess

buildscript {
    def setupRepositories = {
        it.repositories {
            maven {
                url 'https://maven.aliyun.com/repository/public'
            }
            mavenCentral()
            maven {
                url 'https://mirrors.honoka.de/maven-repo'
            }
        }
    }
    setupRepositories(delegate)
    ext {
        def d = delegate
        d.setupRepositories = setupRepositories
    }
}

plugins {
    id 'java'
}

group = 'de.honoka.qqrobot'
version = '1.3.3'

allprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = rootProject.group
    sourceCompatibility = '1.8'

    setupRepositories(delegate)

    dependencies {
        String lombok = 'org.projectlombok:lombok:1.18.26'
        compileOnly lombok
        annotationProcessor lombok
        testCompileOnly lombok
        testAnnotationProcessor lombok
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    //noinspection ConfigurationAvoidance
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        repositories {
            mavenLocal()
            if(project.hasProperty('remoteMavenRepositoryUrl')) {
                maven {
                    //noinspection GroovyAssignabilityCheck
                    url remoteMavenRepositoryUrl
                }
            }
        }
    }

    ext {
        setupVersionAndPublishing = { versionStr ->
            version versionStr

            publishing {
                publications {
                    maven(MavenPublication) {
                        groupId project.group
                        artifactId project.name
                        version versionStr

                        from components.java

                        artifact sourcesJar
                    }
                }
            }
        }
    }
}